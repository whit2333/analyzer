#!/usr/bin/env python
###### Hall A SDK SConstruct Build File #####
###### Author:	Edward Brash (brash@jlab.org) May 2017
###### Modified for Podd 1.7: Ole Hansen (ole@jlab.org) Oct 2018

### There is normally no need to change anything here
### When in doubt, copy the latest version of this file from the SDK directory

import os
import sys

EnsureSConsVersion(2,3,0)

####### Hall A Build Environment #############

halla_dir = os.getenv('ANALYZER')
if halla_dir is None:
    print ("Error: $ANALYZER not defined. Must point to Hall A analyzer installation.")
    Exit(2)
tools_dir = os.path.join(halla_dir,'site_scons')
if not os.path.exists(os.path.join(tools_dir,'configure.py')):
    print("Error: No configure.py in $ANALYZER/site_scons. Check you installation.")
    Exit(2)

baseenv = Environment(ENV = os.environ, tools=['default','symlink','rootcint'],
                      toolpath = [tools_dir])

baseenv.Append(MAIN_DIR = Dir('.').abspath)
print ("Hall A Analyzer main directory = %s" % halla_dir)
print ("Hall A SDK main directory = %s" % baseenv.subst('$MAIN_DIR'))

# Installation support
install_prefix = os.getenv('SCONS_INSTALL_PREFIX')
if not install_prefix:
    install_prefix = os.path.join(os.getenv('HOME'),'.local')
baseenv.Append(INSTALLDIR = install_prefix)
print ('Will use INSTALLDIR = "%s"' % baseenv.subst('$INSTALLDIR'))
baseenv.Alias('install',baseenv.subst('$INSTALLDIR'))

######## Configure Section #######

sys.path.append(tools_dir)
import configure

configure.config(baseenv,ARGUMENTS)
configure.FindROOT(baseenv, need_glibs = False)

conf = Configure(baseenv)
if not baseenv.GetOption('clean') and not baseenv.GetOption('help') \
    and not 'uninstall' in COMMAND_LINE_TARGETS:

    if not conf.CheckCXX():
        print('!!! Your compiler and/or environment is not correctly configured.')
        Exit(1)
baseenv = conf.Finish()

if os.path.exists(os.path.join(halla_dir,'include','ha_compiledata.h')):
    # Installation location
    baseenv.Prepend(CPPPATH = [os.path.join(halla_dir,'include')])
    baseenv.Append(LIBPATH = [os.path.join(halla_dir,baseenv.subst('$LIBSUBDIR'))])
else:
    # Build location
    build_dirs = [ os.path.join(halla_dir,'Podd'),
                   os.path.join(halla_dir,'HallA'),
                   os.path.join(halla_dir,'src'),   # Podd 1.6 compatibility
                   os.path.join(halla_dir,'hana_decode') ]
    baseenv.Prepend(CPPPATH = build_dirs)
    baseenv.Append(LIBPATH = build_dirs)

# Link libraries. NB: libdc is already linked against EVIO
baseenv.Prepend(LIBS = ['HallA', 'Podd', 'dc'])

Export('baseenv')

####### Start of main SConstruct ############

SConscript(dirs = ['.'],name='SConscript.py',exports='baseenv')

if 'uninstall' in COMMAND_LINE_TARGETS:
    baseenv.Command("uninstall-scons-installed-files", None, Delete(FindInstalledFiles()))
    baseenv.Alias("uninstall", "uninstall-scons-installed-files")

#######  End of SConstruct #########

# Local Variables:
# mode: python
# End:

